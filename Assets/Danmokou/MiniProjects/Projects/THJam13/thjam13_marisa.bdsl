
const var me = "thj13.marisa";
const var other = "thj13.marisa-retro";
const var modernc1 = { "blue", "pink", "yellow", "green" };

macro data() {
	cfeature<THJam13CustomDataFeature>()
}
macro tintRetro() {
	rgba(1, 1, 1, lerp01(data().RetroMode01Smooth, 0.5, 1))
}
macro tintModern() {
	rgba(1, 1, 1, lerp01(data().RetroMode01Smooth, 1, 0.5))
}
function setColors()::StateMachine {
	return paction 0 {
		poolcontrol "*black*" tint tintRetro()
		poolcontrol { "*purple*", "*teal*", "*green*", "*orange*", "*yellow*", "*red*", "*pink*", "*blue*" } tint 
			tintModern()
	}
}
var sm = pattern({
		bosses({"thj13.marisa", other}, {(1, 0)})
		setuifrom 2
		mixer {
			(2, { 
				("thj13.boss_retro", BPY(data().RetroMode01))
				("thj13.boss_modern", BPY(1-data().RetroMode01))
			})
		} 
	}, {
	phase 0 { } {
		paction 0 {
			position 2 9
			@ other position -2 9
			exec b{
				mine<Enemy>().ReceivedDamageMult = bpy(1-data().RetroMode01)
				mine<Enemy>().HealthbarOpacityFunc = bpy(1-data().RetroMode01Smooth)
			}
			@ other
				exec b{
					mine<Enemy>().ReceivedDamageMult = bpy(data().RetroMode01)
					mine<Enemy>().HealthbarOpacityFunc = bpy(data().RetroMode01Smooth)
				}
			@ other
				diverthp(fromid(me))
			shiftphaseto 2
		}
	}

	phase 0 {
		dialogue
		rootother other -2 2
	} {
		saction 0 {
			executevn $(thj13dialogue4) "d4"
			shiftphase
		}
	}

	phase 30 { 
		type non "Endless Paths Crossing in the Aether"
		hpi 12000 3
		root 2 2
		rootother other -2 2
	} {
		paction 0 {
			setColors()
			gtrepeat({
				wait(1s)
				times(inf)
				preloop b{
					hvar loop = i;
				}
				waitchild
				rv2incr(<360h>)
			}, { 
				move(2, nroffset(pxy(sine(pm1(loop) * -2, 4, t / 4), 0)))
				async("star-*/w", <>, gcrepeat({
					wait(15 / dl ^ 0.4)
					times(inf)
					fortime(2s)
					color modernc1
					sfx "x-fire-star-1"
				}, gsrepeat({
					times(18 * dl ^ 0.4)
					bindlr
					circle
				}, simple(rvelocity(cx(3)), {
					dir(lr * starrot3)
				}))))
			})
			@ other
				gtrepeat({
				wait(1s)
				times(inf)
				preloop b{
					hvar loop = i;
				}
				waitchild
			}, { 
				move(2, nroffset(pxy(sine(pm1(loop) * 2, 4, t / 4), 0)))
				async("dcircle-black/w", <>, gcrepeat({
					wait(20 / dl ^ 0.4)
					times(inf)
					fortime(2s)
					sfx "x-fire-burst-1"
				}, gsrepeat({
					times(18 * dl ^ 0.4)
					circle
				}, s(rvelocity(cx(3))))))
			})
		}
	}


	phase 42 { 
		type spell "Hakkero: Undodgeable Laser!"
		hpi 22000 8
		root 0 3
		rootother other 2 1.5
	} {
		paction 0 {
			poolcontrol "dcircle-*" allowdelete false
			poolcontrol "dcircle-*" destructible false
			//skiptime 4
			//event(playerinvuln(10000))
			setColors()
			sync "powerup1" <> poweraura { 
				boss2(lerp 0 1 t yellow rgba(1,0.9,1,1), yellow) 
			}
			shake 2 4 (smod010(x / 4))
			sfx "x-incoming"
			delay 0.5 sync "gdlaser-*/" <-45> gsr2c 4 {
				color modernc1
				preloop b{
					hvar itr = i
				}
			} laser(null, 2.5, inf, {
				sfx("x-metal")
				s 4
				rotate(0, lssht(0.1, 2.5, 0, (15 + 2 * itr) * t) * pm1(itr))
				tint tintModern()
			})
			delay 3 sync "dcircle-*/b" <> gsr2 2 <> {
				color { "yellow", "black" }
			} simple(none, { scale 3 })
			async "x-boom" <> gcr2 24 12 <> { } sfx
			delay 5 async "star-*/w" <;1.2:> gir2 (150 / dl ^ 0.4) inf <360h> {
				bindlr
			} gcr2 5 (9 + d3d1) <> {
				preloop b{ hvar loop = ir; }
				sfx "x-fire-star-2"
			} gsr2c (20 * dl ^ 0.7) {
				color modernc1
			}  simple rvelocity polartoxy(lr * lerp01(loop, 3, 1.7), 0) {
				dir(lr * starrot3)
			}

			delay 4 @ other paction 0 {
				async "flasharrow-black/w" <0.2;:90> gir2 (360/dl^0.8) inf <360h> {
					bindlr
				} gcr2 (2/dl^0.8) 120 <0.005;:6> {
					fortime(2s)
					sfxif("x-fire-small-1", i % 7 == 0)
				} s tpnrot polartoxy(lerpt(0, 0.9, 1, 3.6), lr * 14 * i)
				async "dot-black/w" <0.2;:90> gir2 (360/dl^0.8) inf <360h> {
					bindlr
				} gcr2 (3/dl^0.8) 90 <0.005;:9> {
				} s tpnrot polartoxy(lerpt(0, 0.9, 3, 1), lr * 17 * i)
				gtrepeat({
					delay 1s
					times(inf)
					wait(6s)
					bindlr
				}, {
					movetarget(4, $(eiosine), pxy(-2.4 * lr, rand(1.5, 2.5)))
				})
			}
		}
	}

	
	phase 39 { 
		type spell "Floating Space Debris"
		hpi 18000 8
		root 0 -4
		rootother other 0 3
	} {
		paction 0 {
			//skiptime 4
			//event(playerinvuln(10000))
			poolcontrol "lcircle-*" allowdelete false
			setColors()
			exec b{
				hfunction close() {
					return sqrt(0.5 + cosine(8, 0.5, namedtimer("phaset").Seconds))
				}
				hvar mper = 8;
				hfunction delta() {
					return (mper * 1s / ((10 + dc) * 1p)) * lerp01(close(), 1.5, 0.8) / (dl ^ 0.3);
				}
			}
			move(inf, nroffset(pxy(3.4 * sine(mper, 1, t) ^^ 1.1, 4 * (0.5 - cosine(mper/2, 0.5, t)))))
			async "gdlaser-*/b" <-2;:> gcr2 delta() inf <> {
				delay 60
				color { "pink", "blue", "red", "yellow", "purple", "green", "orange", "teal" }
				root zero
				preloop b{
					hvar rh = xytopolar(lbeh(mine))
				}
			} gsr2c 1 {
				start b{
					rv2.rx = rh.x + 3;
					rv2.angle = rh.y;
				}
			} gsr {
				bank(zero)
				times(2)
				preloop b{
					rv2.nx *= -1
					rv2.angle = atan(lbeh(fromid(other)) - pxy(rv2.nx, rv2.ny))
				}
			} laser(null, 3, 0.5, { 
				sfx2("x-fire-star-3", "x-laser-on")
				tint tintModern()
				s 2
			})

			@ other paction 0 {
				gtr {
					wait(max(0.6, delta() * 2))
					delay 90
					times(inf)
					bindlr
				} movetarget(0.6, $(eoutsine), b{
					var l1 = loc + rx(0.8, 90 + angleto(lbeh(fromid(me))))
					var l2 = loc + rx(0.8, -90 + angleto(lbeh(fromid(me))))
					mag(l1 - py(2)) > mag(l2 - py(2)) ? l2 : l1
				})
				async "lcircle-black/" <> gcr {
					times inf
					wait(lerp01(close(), 40, 70 / sqrt(dl)))
					sfx "x-fire-burst-1"
					bindlr
				} gsr {
					times 25
					target ang Lplayer
					start b{
						rv2.angle += rand(-0.5, 0.5) * 9 + sine(3, 5, i)
					}
					rv2incr(angle(lerp01(close(), 3, 6 / sqrt(dl))))
					center
					preloop b{
						hvar myscale = lerp01(close(), 0.7, 0.35)
					}
				} simple rvelocity px(lerpt(0.4, 1, 4, 0.7)) {
					scale myscale
					dir(170 * lr * t) 
				}
			}
		}
	}

	phase 60 {
		type final "Upon the Gears of the Universe"
		hpi 29000 10
		root 0 -3.6
		rootother other 0 3.6
	} {
		paction 0 {
			setColors()
			exec b{
				hvar mvper = 20
				hfunction updateRot(t, y) {
					return (12f + limit(1, (y - 1.7) / 1.4) * -16f) * lerp(1, 4, t, 0.1, 1);
				}
			}
			sync "powerup1" <> poweraura { 
				boss2(lerp 0 3 t yellow rgba(1,0.9,1,1), yellow) 
			}
			@ other
				sync "powerup1" <> poweraura { 
					boss2(lerp 0 3 t yellow purple, pink) 
				}
			shake 2 4 (smod010(x / 4))
			sync "gdlaser-*/" <> gsr2c (4+d3d1) {
				root zero
				color { "orange", "green", "yellow", "blue", "pink" }
				preloop b{
					hvar rot = 30;
				}
			} laser(nroffset(lbeh(mine)), 3, inf, {
				sfx2("x-incoming", "x-metal")
				rotate(0, b{
					rot += updateRot(t, y)
				})
				tint tintModern()
			})
			sync "gdlaser-black*" <> gsr2c (4+d3d1) {
				root zero
				color { "/", "/w" }
				preloop b{
					hvar rot = 30;
				}
			} laser(nroffset(lbeh(fromid(other))), 3, inf, {
				sfx2("x-incoming", "x-metal")
				rotate(0, b{
					rot += updateRot(t, y)
				})
				tint tintRetro()
			})
			delay 2.9 move inf nroffset(ptmul pxy(1, 1.2) circle(mvper, 3, t - mvper/4) - py(-3.6))
			delay 2.9 @ other
				move inf nroffset(ptmul pxy(1, 1.2) circle(mvper, 3, t + mvper/4) - py(3.6))
			delay 4 @ other async "flasharrow-black/" <-1;:> gcr {
				sfx "x-fire-burst-2"
				wait lerp(-3.6, -2, y, 60 / dl^0.1, 120/dl^0.4)
				times inf
			} gsr2 1 <> {
				target rang Lplayer
			} gsr2 3 <;0.8:> {
				center
			} gsr2 5 <20> {
				center
				bank0(zero)
				sltarget rang Lplayer
			} s rvelocity px 2.2
			delay 4 async "star-*/" <0.5;:> gcr {
				wait lerp(-3.6, -2, y, 3 / dl ^ 0.1, 6 / dl ^ 0.8)
				times inf
				sfxif "x-fire-star-2" (t % 4 == 0)
				preloop b{
					var rg = 800;
					var mi = t % rg;
					var delta = 6 / dl ^ 0.8;
					if (mi > rg * 0.5) {
						delta *= -1;
						mi = mi - rg * 0.5;
					}
					if (mi > rg * 0.3 && mi < rg * 0.5) {
						delta *= cosine(rg * 0.4, 1, mi - rg * 0.3);
					}
					rv2.angle += delta;
				}
				color modernc1
			} gsr2c 4 {

			} simple rvelocity px 1.5 {
				dir starrotb4
			}
		}
	}


	phase 0 {
		dialogue
	} {
		saction 0 {
			wait 2
			executevn $(thj13dialogue5) "d5"
			~ @ other
				movetarget(3, $(einsine), pxy(8, 7))
			movetarget(3, $(einsine), pxy(-8, 7))
			shiftphase
		}
	}

})
sm;