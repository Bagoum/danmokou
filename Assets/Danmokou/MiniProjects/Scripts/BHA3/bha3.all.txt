<#> warnprefix
pattern { 
	boss bha3.elly
	bgm {
		2 bha3.bgm
	}
	setuifrom 2
}

!!{ me elly

phase 0
	paction 0
		shift-phase-to 10


!{ power1()
sync powerup1 <> powerup2
	x-powerup-1 x-powerdown-1
	witha 0.9 lerp 0 1 t orange red
	witha 0.9 yellow
	1.5 2 0 0.5
!}
!{ power1atk()
gtr {
	sequential
} {
	$power1
	wait-for 1.2
	anim attack
	wait-for 0.3
}
!}
!{ power1long()
sync powerup1 <> powerup x-powerup-1
	witha 0.9 lerp 0 2.5 t red yellow
	3 5
!}
!{ yuukapower1()
sync powerup1 <> powerup2
	x-powerup-1 x-powerdown-1
	witha 0.9 lerp 0 1 t green red
	witha 0.9 green
	1.5 2 0 0.5
!}



##1
<!> showlives 0
<!> dialogue #{
<!> root 0 1
phased bha3.1
#}

!!{ scytherot dir(-1200 * t)

##2
<!> type non :boss.elly.dark #{
<!> hpi 14000 6
<!> root 0 2
phase 35
	paction 0
!!{ scythet1 4
!!{ scythet2 5
!!{ sunt 2
		bullet-control persist empty.scythe cull(> t $scythet2)
		bullet-control persist ellyscythe cull(> t $scythet2)
		bullet-control persist sun-* cull(> t $sunt)
		gtr {
			wait-child
			wait 1s
			times _
			sequential
			bindLR
			p this
		} {
			~ move-target 1.5 io-sine pxy(&lr * rand 0.9 1.3, rand 1.7 2.2)
			##wait-for 0.5
			$power1atk
			sync _ <> darkness(Lplayer, lerpsmooth(out-sine, 1.8, 4.5, t, 10, 
																lerpsmooth(in-sine, 7, 9, t, 1, 10)), rgba(0, 0, 0, lerpt(1.8, 3.7, 0, 1)))
			sync ellyscythe <> gsr {
				root zero
			} guideempty .scythe p { sloc loc } { } #{
					nroffset(Lbeh $me + tp qrotate px(40) rx(lerpt3(0, 1.5, $scythet1, $scythet2, 0, 2, 0), 180p * &lr * t + brand(0, 360))) {
						inode nroffset @ sloc p
							async _ <> gcr {
								times(8)
								delay(80)
								wait(40)
								for(1s * $scythet2)
								p this
								color({ *-pink* *-red* *-orange* *-yellow* })
							} {
								color sun color / simple null {
									scale(lerp(0.6, 1, t / $sunt, 0.6, 0))
								}
								gsr2c(18 * dl ^ 0.5) {
									target a Lplayer
									offset angle rand(0, 360)
								} {
								!!{ lasert (3.2 - 20f * p)
									color gdlaser color /b laser null $lasert 0  {
										dsfx
										tint(rgba(1, 1, 1, lerp(0.6, 1, t / $lasert, 1, 0)))
									}
									color amulet color / gsr {
										times 6
										p add
									} s tprot px lerpsmooth(in-sine, 0.4, 4.5 - 30f * p1, t, 0, 4 - 0.2 * p2)
								}
							
							}
							
						simple nroffset @ sloc p  {
							$scytherot
							scale(lerpt($scythet1, $scythet2, 1, 0))
						}
					} #}
		
			async dot-*/w <0.9;:> gcr {
				wait(2s)
				times(4)
				sfx x-fire-burst-1
			} gsr2c 4 {
				offset(angle rand(0, 360))
				bindLR
				color({ red yellow })
			} gsr2c(30 * dl) {
				bank(<>)
			} s polar(
				lssht(-2, 0.5, 2 * t, 1 * t),
				8 * t * &lr
			)
		}
#}


##3
<!> type spell :boss.elly.thief #{
<!> hpi 14000 5
<!> root 0 2
phase 42
	paction 0
		savef timerate 0 1
		bullet-control persist {{ sakura-red* sakura-green* sakura-orange* }} updatef {
			elapsed (&.elapsed + 1f * if(> &.dofast 0, @0(timerate), 1))
		} _
		async sakura-green/w <> gcr2 150 _ <> {
			sfx x-fire-burst-1
		} gsr2c(dl * 15) {
			offset(aphi(&times)* &pi )
		} gsr2 if(> dn 1, 5, 4) <2> {
			center
			start {
				elapsed =f 0
				dofast =f 0
			}
			expose {
				f dofast
			}
		} s roffset(px(1.2 * &elapsed))

		sync ellyscythe <> gsr {
				root zero
			} guideempty .scythe p { sloc loc } { } 
					nroffset(Lbeh $me + py(0.5) + tp qrotate px(40) rx(lerpt(0, 1, 0, 1.5), 180h * t)) {
						inode nroffset @ sloc p
							async amulet-yellow/ <-0.1;:> gcr2 lerpd(60, 50) _ <> {
								delay 3s
								sfx x-fire-small-1
							} gsr2c(dl * 20) {
							 offset(aphi(&times) * &pi)
							} s rvelocity px(3.2)
							
						simple nroffset @ sloc p  {
							$scytherot
						}
					} 
		
		gtr {
			bindLR
			sequential
			wait-child
			wait(3s)
			times(_)
			p this
		} {
			~ move-target 2 io-sine pxy(&lr * rand(0.8, 1.2), rand(1.9, 2.4))
			wait-for 0.5
			sfx x-transform-3
			bullet-control once sakura-green/w batch _ {
				updatef dofast 1 _
				restyleeffect sakura-orange/w cwheel-orange/ _
			}
			$power1atk
			~ 	async lightning-yellow/ <90> gcr {
				wait(24)
				times(6)
				bindLR
				p this
				frv2(angle(&lr * (50 - 10 * (p // 2))))
				sfx	{ x-lightning }
			} pather 1 0.4 tpnrot(
					truerotatelerprate(lerpt(0.2, 0.5, 300, 120), rotify cx 1,
						ss 0.4 vhome 2 Lplayer)
				* lerp3 0.0 0.3 1.1 1.3 t 16 2 8)
				{ s 2 high }
			async _ <> gcr {
				wait(1)
				times(0.5s)
				savef(timerate 0 lerp(0, 0.2, softmod(0.5, t / &times), 1, lerp(0, 3, p, 3, 8)))
			} noop
			savef timerate 0 1
			bullet-control once sakura-orange/w restyleeffect sakura-red/w cwheel-red/ _
		}
#}


##4
<!> type non :boss.elly.galileo #{
<!> hpi 13000 10
<!> root 0 2
phase 42
	paction 0
!!{ delay 1.5
!!{ pt timersec(phaset)
!{ stand(pr, itr)
pxy(0.3 * (0.2 + 0.8 * sqrt(%pr)) * sin(1.2 * $pt + 6 * %pr + tau * 1h * %itr), 8.8 * %pr)
!}
!!{ ry -5
!!{ numstalks 5
		async gdlaser-green/b <> gir {
			root(cy($ry))
			center
			wait(24)
			times($numstalks)
			rpp(<1.5;:>)
			p this
			preloop {
				stalkr =f (t / &times)
				stalkir =f ((--- &times t) / &times)
			}
			expose {
				f stalkr
				f stalkir
			}
		} {
			laser null $delay 1 {
				dsfx
				straight(90)
			}
			
			gir {
!!{ drawwait 4
				delay(1s * $delay)
				preloop {
					startt =f &st
				}
				wait($drawwait)
				sequential
!!{ stalkn(ceil(22 * dl ^ 0.6))
!!{ cutr 0.6
!!{ cutcenter 0.9
!!{ stalkstayn floor($stalkn * $cutr)
!!{ stalkcutn ($stalkn - $stalkstayn)
			} {
				gcr {
					color gcircle-green/.1
					wait($drawwait)
					times($stalkstayn)
					preloop {
						pr =f (t / $stalkn)
					}
				} simple nroffset $stand(&pr, p) {
				}
				
!!{ cutper (lerp(5, 14, timersec(phaset), 4, 3) * lerpd(1.2, 1))
				gir {
					times(_)
					wait(1s * $cutper)
					p add
					start cutt =f -1
				} {
!!{ per 3pp
!!{ cutactiont 0.5
!{ nodefallt(cutt)
 ($pt - %cutt - $cutactiont * if(divby(2, p2), &.stalkr, &.stalkir))
!}
!{ leafrot()
	if(< &.cutt 0, 0, lerpsmooth(out-sine, 0, 3, $nodefallt(&.cutt), 0, brandpm1 * brand(40, 70)))
!}
					guideempty .leaf p { pl loc } { cutt &.cutt  pd $leafrot } nroffset($stand($cutcenter, p1) + :: {
							t	$nodefallt(&cutt)
						} if(< &cutt 0 | < &t 0, zero, 
							pxy(sine($per, 1 * pm1(p2), &t), 0 - 0.8 * &t - 0.23 * (cosine($per / 2, 1, &t) - 1)))
						) {
						##Debug
						##color lcircle-red/w s nroffset @ pl p
					}
				
					gcr {
						color gcircle-green/
						wait($drawwait)
						times($stalkcutn)
						preloop {
							pr =f ((t + $stalkstayn) / $stalkn)
						}
					} loc0 simple nroffset(@ pl p + rotate(@ pd p, $stand(&pr, p1) - $stand($cutcenter, p1))) {
					}
				}
			}
		}
		_ ($delay - 1.5) $power1atk
		
!!{ sunper lerpd(9, 7)
!{ sunperblock(t)
	((%t + $sunper / 4) // ($sunper / 2))
!}
		bullet-control persist sun-orange/ batch (neq(&.lastSummonRd, $sunperblock(t)) & < abs(x - [Lplayer].x) 0.1) {
			updatef lastSummonRd $sunperblock(t) _
			sm _ gtr { } {
					sfx x-metal
					sync zonelaser-red/b <-2;:-90> safelaser 3.2 { }
					sync apple-red/ <> simple nrvelocity py lerpt(0, 1.5, 0, -3.2) { 
						scale((3 + sine(4h, 0.3, t)) * lerpd(0.8, 1))
						dir(-90 + sine(5h, 24, t))
					}
				}
		}
		sync sun-orange/ <> gsr {
			root zero
			start lastSummonRd =f 0
			expose f lastSummonRd
		} simple nroffset(lerpsmooth(out-sine, 0, 1, t, Lbeh $me, pxy(sine($sunper, 5, t), 3.2))) {
			scale(1)
		}
		
		bullet-control persist gcircle-green* restyleeffect circle-yellow/ cwheel-yellow/
			(> @ cutt p 0 & > $nodefallt(@ cutt p) 0)
		gtr {
			wait(1s * $cutper)
			times(_)
			delay(3s)
			sequential
			p this
			bindLR
		} {
			sync ellyscythe <> simple rvelocity lerpt(0, 0.3, zero, pxy(&lr * -4.2, 1)) { $scytherot }
			~ _ 1 sync ellyscythe <> gsr {
				root(pxy(-6 * &lr, $ry + .y $stand($cutr, 0)))
			} simple rvelocity px if(> t 0.4, &lr * 14, 0)  { $scytherot }
			~ move-target 1.5 out-sine pxy(&lr * rand(1.2, 1.4), rand(1.6, 2.4))
			$power1atk
			bullet-control once empty.leaf updatef {
				cutt $pt
			} < &.cutt 0
			~ async x-transform-3 <> gcr {
				wait(1s * $cutactiont / $numstalks)
				times($numstalks)
			} sfx
		}
	
#}


##5
<!> type spell :boss.elly.reaper #{
<!> hpi 14000 6
<!> root 0 2
phase 42
	paction 0
		pool-control ellyscythe allowcull false
		pool-control empty.scythe allowcull false
		bullet-control persist empty.scythe updatev2 {
			vel	lerpt(0.6, 1.4, &.vel, (&.vel + (Lplayer - loc - &.vel * 0.5) * 0.5f * clamp(1.6, 3, dist(Lplayer, loc))))
			loc	(&.loc + &.vel * 1f)
		} _
		
		gtr {
			delay 1s
			wait(5s)
			times(_)
			bindLR
			p this
		} move-target 2.5 io-sine pxy((mod(3, p + 2) - 1) * rand(1.7, 2), if(= mod(3, p) 2, 2, 1.5))
		
		async ellyscythe <> gcr {
			preloop {
				vel =v2 pxy(-2 * pm1(p), -0.4)
				loc =v2 loc
			}
			expose {
				v2 vel
			}
			root zero
			sfx x-metal
			p this
			wait(18s)
			times(2)
		} guideempty .scythe p { sl1 loc } { } nroffset(&loc) {
			simple nroffset @ sl1 p {
				$scytherot
				scale(1)
			}
			color lightning-purple/ pather 2 lerpd(1, 2) nroffset @ sl1 p {
				s(2)
			}
			color sun-purple/w simple nroffset @ sl1 p {
				scale(0.5)
			}
		}
		async ellyscythe.1 <> gcr {
			root zero
			p this
			times(2)
			delay(2s)
			wait(4hs)
			sfx x-metal
		} guideempty2 p { sl loc } nroffset(lerpsmooth(out-sine, 0, 1, t, Lbeh($me), pxy(sine(8h, 3.5, t), 3))) {
			inode nroffset @ sl p
				async apple-*/ <90> gcr {
					wait(30 / dl)
					times(_)
					delay(60)
					colorf({ green red }, p)
				} simple velocity(px(2), py(lerpt(0.4, 3, 0, -6))) {
					dir(-90 + sine(1, 14, t / 4h + brand(0, 1)))
					scale(1 + sine(1, 0.07, t / 5h + brand(0, 1)))
				}
			color apple-*/ gsr {
				colorf({ green red }, p)
			} simple nroffset @ sl p {
				scale(2 + sine(4h, 0.2, t))
				dir(-90 + sine(5h, 20, t))
			}
			simple nroffset @ sl p {
				$scytherot
				scale(1)
			}
		}
		
		async sakura-*/ <> gcr2 (lerp(0, 20, timersec(phaset), 36, 32) / dl) _ <> {
			p this
			sfx x-fire-small-1
		} gsr2 2 <> {
			bindLR
			color { yellow orange }
		} gsr2c 6 {
			offset(angle(8.903 * &lr * p))
		} s rvelocity
			px lerpt3(0.7, 0.84, 1, 3, 2, 0, 3.3)
		
#}


##6
<!> type non :boss.elly.canyon #{
<!> hpi 15000 8
<!> root 0 0
phase 42
	paction 0
		$power1atk
	paction 0
		async lstar-*/ <4.2;:> gir2 60 _ <120h> { wait-child sfx x-metal } gcr {
			wt	0.5 2300
			frv2	angle pivot t 740 (1 * (t / 2) ^ 0.95)
						pivot t 980
							sine(480, 18, t - 720)
							(-0.04 * ((t - 860) / 2) ^ 1.7)
			sfxif(x-fire-star-2, divby(16, t) & in t 650 1100)
		} gsr2 3 <120> { root zero color { red orange yellow } }  simple tp-rot cx 1 {
			dir(starrotb2)
			scale(0.4)
		}
	
!!{ over lerpd(7, 6)
		async sakura-*/w <> gcr2 ($over / 4f) 4 <> { 
			p this
			color { orange yellow }
			sfx x-fire-burst-1
		} gsr2c(14 * dl ^ 0.6) {
		} gsr2 4 <2> {
		} simple polar(
			4.1 * esine010(t / $over),
			lerpd(5, 8) * pm1(p) * t) {
				scale(1)
			}
			
	!!{ zrpt (2 + 2 * dc)
		gtr {
			wait(3s)
			times(2)
			rpp(<360h>)
			sequential 
			p this
			color({ red green })
			bindLR
			delay(1s)
		} {
			$power1atk
			sync _ <;:> gsr2c $zrpt {
			} {
!!{ rot (&lr * t * (11 + 1.7 * &lr))
				colorr zonelaser-*/b safelaser _ { rotate 0 $rot }
!!{ crpt 4
!!{ per (11 + 1.7 * &lr)
!!{ softmodT (t / $per + 2 * p2 / $crpt + p1 * 1h)
!{ polarMov()
polar(
	lerpsmooth(out-sine, 0, 5, t, 0, 
		4.3 * softmod(1, $softmodT)
		##2 * ++ sine($per, 1, t + p2 * $per / $crpt + p1 * 1h * $per)
	),
	$rot)
!}
				gsr2 $crpt <> {
					##TODO: apple bullets
					colorr apple-*/
					p add
					bindAngle
				} {
					simple $polarMov {
						scale(1 + sine(1, 0.07, t / 5h + brand(0, 1)))
						dir(&angle + $rot + sine(3p, 10, t) + 180 * z1(floor($softmodT)))
					}
					gsr {
						resetcolor
						color ellyscythe
					} simple $polarMov {
						dir(-1200 * (t + brand(0, 1)))
						scale(0.35)
					}
				}
			}
		}
#}


##7
<!> type spell :boss.elly.lycidas #{
<!> hpi 14000 14
<!> root 0 1
phase 54
	paction 0
		$power1atk
	paction 0
!!{ circColors { *-orange/w *-red/w }
		bullet-control persist  { { gcircle* } $circColors } cull > t 0.3
		pool-control { { gcircle* } $circColors } tint rgba(1, 1, 1, lerpt(0.16, 0.3, 1, 0))
		bullet-control persist { { glcircle* } $circColors } cull > t 0.7
		pool-control { { glcircle* } $circColors } tint rgba(1, 1, 1, lerpt(0.5, 0.7, 1, 0))
!!{ w 3.54
!!{ wh ($w / sqrt(12))
!!{ s ($w / sqrt(3))
!{ hexpos(x)
	lerpmany({ 
		0 cxy(0, 0)
		1 pxy(0, $s)
		2 pxy($w / -2, $s + $wh)
		3 pxy(neg($w), $s)
		4 pxy(neg($w), 0)
		5 pxy($w / -2, neg($wh))
		6 cxy(0, 0)
	}, mod(6, %x))
!}
!!{ tseg 34
		async gstretchlaser-*/w <> gcr {
			root(zero)
			times(_)
			wait($tseg)
			p this
		} gsr2 2 <> {
			bindLR
		} gsr2 2 <> {
			bindUD
			color { orange red }
			frv2(v2v2f(mulentry(pxy(&lr, &ud), $hexpos(p1)), zero, 0))
			p add
		} laser null 14f (1f * $tseg) {
			straight(&ud * (90 + &lr * 60 * p1))
			length(2)
			s(5)
		}
!!{ segsplit 8
		async _ <> gcr {
			root(zero)
			times(_)
			wait($tseg / $segsplit)
			p this
		} gsr2 2 <> {
			bindLR
		} gsr2 2 <> {
			bindUD
			color $circColors
			frv2(v2v2f(mulentry(pxy(&lr, &ud), $hexpos(p / $segsplit)), zero, 0))
			alternate(if(= mod($segsplit, p) 0, 1, 0))
		} { 
			color gcircle s null
			color glcircle simple null {
				scale(lerpt(0.45, 0.65, 0.5, 0))
			}
		}

!!{ delay 4
		async gdlaser-*/b <-6;:-90> gcr {
			delay(1s)
			times(20)
			wait(15)
			colorf({ yellow red orange }, rand(0, 3))
			frv2(angle(rand(-10, 10)))
		} laser null ($delay - &st * 0.8) 2 {
			dsfx
			s 2
		}
		$power1long()
		_ 1 $power1long()
		_ 1.8 $power1long()
		_ 2.5 $power1long()
		_ 3 $power1long()
		_ 1 draiko 1 $delay
		gtr {
			delay(1s * $delay)
		} {
			draiko 3 4
			async x-boom <> gcr3 18 3s <> {} sfx
			savef ang 0 -90
			savef delta 0 0
			sync sun-yellow/b <> gsr {
				root zero
			} simple nroffset Lbeh $me {
				scale 0.7
			}
			async _ <> gcr2 1 _ <> {
				delay 3.5s
				savef {
					delta 0 degintorange(atan(Lplayer - loc) - @0 ang)
					ang 0 (mod(360, @0 ang + lerpt(0, 2s, 0, lerp(60, 180, abs(@0 delta), lerp(20, 40, timersec(phaset), 0.1, 0.14), 0.28)) * sign(@0 delta)))
				}
			} noop
			async sakura-*/b <;:> gcr3 0.2 _ <> {
				preloop {
					itr =f mod(1024, t)
				}
				color { orange }
				frv2(angle(@0 ang + rand(-5, 5)))
			} simple roffset(
				px(8 * t) +
				circle(0.9, (0.4 + sine 20p 0.3 &itr),  pm1(&itr) * t + 2 * modh &itr)) {
				}
			move-target 1 out-sine cy 2.8
			_ 2 move-target 1.5 io-sine pxy(sign([Lplayer].x) * -2, 0)
			_ 5 move _ nroffset(lerpsmooth(io-sine, 0, 3, t, zero, rx(-1.5, @0 ang) - ss0(loc)))
			_ 4 async fireball-yellow/ <> gcr {
				wait(42 / dl)
				times(_)
				rpp(angle(if(> sign(@0 delta) 0, -10.27, 10.29)))
				sfx x-fire-small-1
			} gsr2c (4 + d3d1) {
			} s tprot cx 2.1
!!{ drop (60 / dl)
			_ 12 async ellyscythe.1 <> gcr {
				root zero
				sfx x-metal
				times(2)
				wait(blockround($drop, 960))
				p this
			} guideempty2 p { sl loc } nroffset(lerpsmooth(out-sine, 0, 3, t, Lbeh($me), rx(3.8 - 3.3 * p, pm1(p) * 30 * (1 + 1h * p) * t + ss0(@0 ang)))) {
				inode nroffset @ sl p
					async apple-*/ angle(-90 + 180 * p) gcr {
						wait($drop)
						times(_)
						face v
						colorf({ green red }, p)
						sfx x-fire-small-2
					} simple rvelocity(px(lerpt(0, 2, 0, -2.5))) {
						dir2(rotate(sine(1, 14, t / 4h + brand(0, 1)), norm(accdelta)))
						scale(1 + sine(1, 0.07, t / 5h + brand(0, 1)))
					}
				color apple-*/ gsr {
					colorf({ green red }, p)
				} simple nroffset @ sl p {
					scale(2 + sine(4h, 0.2, t))
					dir(-90 + sine(5h, 20, t))
				}
				simple nroffset @ sl p {
					$scytherot
					scale(0.8)
				}
			}
		}
#}		


##8
<!> type spell :boss.elly.mountain #{
<!> hpi 13000 9
<!> root 0 2.3
phase 42
	paction 0
		bullet-control persist empty.seed sm onlyonce offscreen loc
!!{ stayt 2.5
!!{ flowerstayt 5.5
			gtr { } {
				_ ($flowerstayt + 0.1) sfx x-boom
				async sakura-green/ angle(180 - 14 * pm1(p)) gir {
	!!{ outerwait 3
					wait $outerwait
	!!{ nbranch 7
					times(2 + ($nbranch * (3 + dc)))
					preloop {
						pr =f (t / &times)
						dobranch =f if(divby(&times // $nbranch, t) & > t 0, 1, 0)
						branchtype =f (t // (&times // $nbranch))
					}
					bindItr stalk
					saoffset(bt, 0, pxy(
								7.3 * &pr,
								0.5 * &pr ^ 0.8 * sin(6 * &pr + tau * 1h * p)))
					sfx x-bubble
				} { 
					s rvelocity rotate(-10p * &stalk, px(lerp(0, 1, t - $stayt + 0f * $outerwait * &stalk, 0.01, 1) * 2))
					gir {
						alternate(&dobranch)
					} {
						noop
						gcr {
							color sakura-green/
	!!{ innerwait 3
							wait $innerwait
							times(14 * dl ^ 0.7)
							start {
								rot =f  mp1(&branchtype)
								initial =f rand(3.1, 3.5)
							}
							preloop {
								pr2 =f (t / &times)
								pr2i =f ((--- &times t) / &times)
							}
							bindItr branch
	!{ spiral(x)
	rx(
		-0.2 + 1.4 * %x,
		raddeg(&initial * %x)
		)
	!}
							saoffset(bt, 0, rotate(90 + 180 * &branchtype, 
								multiplyy(&rot, $spiral(&pr2i) - $spiral(1))))
								
							alternate(if(= t -- &times, 1, 0))
						} {
							s rvelocity px(lerp(0, 1, t - $stayt + 0f * $outerwait * &stalk + 0f * $innerwait * &branch, 0, 1) * 1.5)
							gsr2c(10 * dl) {
								bank <0.3;:>
								color sakura-black/w
							} s rvelocity px(lerp(0, 0.5, t - $flowerstayt + 1f * $outerwait * &stalk, 0, 2.5))
						}
					}
				}
			}
	
		gtr {
			wait(lerp(0, 14, timersec(phaset), 6s, 5s) * lerpd(1.2, 1))
			times(_)
			bindLR
			p this
			sequential
		} {
			~ move-target 1.5 out-sine pxy(&rl * rand(1.0, 1.2), rand(2, 2.5))
			$power1atk
			sync ellyscythe <> gsr {
				target a pxy(3.7 * &lr, -4)
			}  guideempty .seed p { pl loc } { } rvelocity(px(lerpt(0.2, 0.4, 0, 6))) {
				simple nroffset @ pl p {
					$scytherot
					scale(1)
				}
				color lightning-red/w pather 2 2 nroffset @ pl p {
					s(2)
				}
				color sun-yellow/ simple nroffset @ pl p {
					scale(0.5)
				}
				inode nroffset @ pl p 
!!{ dropwait (15 / dl ^ 0.7)
					async amulet-** <90> gcr {
						face v
						delay(0.3s)
						times(_)
						wait($dropwait)
						while(onscreen loc)
						p this
						sfx x-fire-small-1
					} gsr2c 2 {
						color { yellow orange }
					} gsr2 2 <30> {
						center
						color { / /b }
					} gsr2 (if(> dn 1, 2, 1)) <> {
						preloop itr =f --- &times t
					} s rvelocity px lerp(1.3, 1.8, t + 1f * $dropwait * p, 0, 1.6 - 0.1 * &itr)
			}
		}
#}


##9
<!> type final :boss.elly.kepler #{
<!> hpi 40000 15
<!> root 0 2
<!> spellcutin 1
phase 69
	paction 0
		pool-control moon* allowcull false
		sync sun-yellow/ <> gsr {
			sfx x-metal
		} simple null {
			scale 0.8
		}
!{ guide(color, rad, len)
sync gdlaser-%color%/b <> laser null _ 0 {
			dsfx
			length(%len)
			static(polar(
				%rad,
				360 * t / %len
			))
		}
!}
!!{ r1 1.5
!!{ r2 2.8
!!{ r3 4.2
!!{ r4 6
		$guide(red, $r1, 6)
		$guide(orange, $r2, 10)
		$guide(blue, $r3, 14)
		$guide(red, $r4, 18)
!{ planet(index, color, opts, rad0, rad, orbit, sm)
	sync %color% <> guideempty2 %index { pl loc } polar(
		%rad,
		%rad0 + 50 * lssht(1, 2, 0, t) / %orbit
	) {
		simple nroffset @ pl %index %opts
		inode nroffset @ pl %index
			%sm
	} 
!}
!!{ smul 0.8
!!{ or1 .25
!!{ or2 .62
!!{ or3 1
!!{ or4 -1.62
		$planet(0, sun-red/b, { scale($smul * 0.24) }, 90, $r1, $or1, _ 8 gtr { } {
				$power1
				@ $me $power1atk
				_ 1.5 async gem-yellow/w <> gcr {
					wait(15 / dl)
					times(_)
					face velocity
					sfx x-fire-small-1
				} gsr2c 2 {
				} s tprot px(lerpt(0, 0.4, 1, 1.5))
			})
		$planet(1, sun-orange/, { scale($smul * 0.5) }, 0, $r2, $or2, _ 17 gtr { } {
				$power1
				@ $me $power1atk
				_ 1.5 async fireball-orange/ <> gcr {
					wait(12 / dl ^ 0.8)
					times(_)
					rpp(angle(17p / dl ^ 0.8))
					sfx x-fire-small-2
				} s tprot px(lerpt(0, 0.5, 1.1, 2.1))
			})
		$planet(2, sun-blue/, { scale($smul * 0.54) }, -140, $r3, $or3, _ 30.5 gtr { } {
				$power1
				@ $me $power1atk
				_ 1.5 async apple-green/ <90> gcr {
					wait(32 / dl)
					times(_)
					face v
					sfx x-fire-small-3
				} gsr2c 2 {
				} s tprot px(lerpt(0, 2, 0, 3.2))
			})
		$planet(3, sun-red/, { scale($smul * 0.35) }, -50, $r4, $or4,  _ 37 gtr { } {
				$power1
				@ $me $power1atk
				_ 1.5 async lightning-red/ <> gcr {
					wait(120 / dl)
					times(_)
					sfx x-lightning
					face derot
				} gsr2 1 <40> {
					target a Lplayer
					center
				} pather 0.5 0.5 tprot px lerpt 0.3 0.6 1.5 2.7 {
					s(2)
					high
				}
			})
		
		savev2 yuuka 0 cxy -1 8
		sync _ <> gsr {
			root(cxy(-1, 8))
		} inode null gtr { } { 
			move-target 2 out-sine cxy(-2, 3)
			async _ <> gcr2 1 _ <> {
				savev2 yuuka 0 loc
			} noop
			gtr {
				wait(lerp(0, 30, timersec(phaset), 4s, 0.0s))
				wait-child
				delay(2s)
				times(_)
				p this
			} saction 0
!!{ crosshairt  (lerp 0 6 p 4 lerpd(3.8, 3))
				~ _ ($crosshairt - 1) $yuukapower1
				~ _ 0.5 sync zonelaser-yellow/b <> laser nroffset rx -10 angleto @0 sploc $crosshairt 0 {
					rotate 0 angleto @0 sploc
					length 30
					s(4)
				}
				crosshair crosshair lerpsmooth(out-sine, 0, 59, t, loc, Lplayer) 0.5 $crosshairt sploc 0
				move-while 1 onscreen loc tpnrot ss0 vhome 50 @0 sploc
				sync x-boom <> sfx
				~ draiko 1 0.6 
				break
		}
		sync sun-black/w <> gsr {
			root zero
		} simple nroffset(@0 yuuka + pxy(
			sin(3 + 1.8 * 1.54 * t),
			cos(4 + 1.8 * 2 * t)
		) * 0.14) {
			scale(lerpd(0.7, 1))
		}
		

#}

##10
<!> dialogue #{
<!> root 0 1
phased bha3.2
#}

<!> root 0 8
phase 0
	paction 0
		shift-phase

///


