<#> bdsl2
pattern { } {
phase 0 { } {
	paction 0 {
        position 1 0
		shiftphaseto 2
    }
}
	
phase 0 {} {
	paction 0.1 {
		bulletcontrol persist "empty" savev2 {
			 (h("myloc"), code(p), code(loc))
			 (h("mydir"), code(p), code(dir))
		} _
		bulletcontrol persist "empty" savef 
			 (h("mydir"), code(p), code(dir)) 
		 _
		sync "enemy01" <1;:45> gsr2 4 <;:90> { p this } summon tprot cx 1
			async "empty" <0.3;:> gcr2 120 4 <-90> { p add } {
				s tprot rotatelerprate 90 cx 1 cr 1 90
				loc0c "gem-red/b" gsr2 10 <30> {} s offset 
					zero
					rh "myloc" p
			} { }
    }
}

phase 0 {} {
	paction 0.1 {
		//Simplified syntax: one command for all guideing
        async "strip-teal/w.2" <1;:> gcr2 24 inf <;:15> { p this } guideempty ".1" p { ("pl", code(loc)), ("pd", code(dir)) } { }
			rvelocity rotatelerppercent 0.3 cx 1 cy 1 //empty guide movement 
			{
				gsr2c 20 { } s doffset "pl" "pd" p
					regpoly 0.4 5 angle
				color "ellipse-yellow/w" s nroffset rh("pl", p)
				color "ellipse-orange/w" s dtpoffset "pl" "pd" p
					pxy -0.15 -0.15
			}
		//Legacy/expanded syntax
		
		bulletcontrol persist "empty" savev2 {
			 (h("myloc"), code(p), code(loc))
			 (h("mydir"), code(p), code(dir))
		} _
		bulletcontrol persist "empty" savef {
			 (h("mydir"), code(p), code(dir))
		} _
		
        async "empty" <1;:180> gcr2 24 inf <;:15> { p this } s rvelocity rotatelerppercent 0.3 cx 1 cy 1
		async "ellipse-red/w" <> gcr2 24 inf <> { p this } world s offset(zero,
			rh "myloc" p)
		async "ellipse-purple/w" <> gcr2 24 inf <> { p this } world s offset(zero,
			rh "myloc" p +
			rotatev rh "mydir" p cxy -0.15 -0.15)
		async "strip-green/w" <> gcr2 24 inf <> { p this } gsr2c 20 { p add } world s offset(zero,
			rh "myloc" p1 +
			polartoxy(
				regpolyr 0.4 5 degrad(18 * p2),
				18 * p2 + rh "mydir" p1
			))
		
    }
}
				
}    
