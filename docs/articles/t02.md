# Tutorial 2: Bullet Controls

![Unity_SCkxEkxGVQ](../images/Unity_SCkxEkxGVQ.jpg)

## Part 1: Flipping

Go back to DMK Tutorial Setup 00. We'll use the same file.

Drop this code in at the bottom and run the script: (Note: I have removed some parentheses. Recall that parentheses and argument separators are optional.)

```python
		bullet-control(persist, { { circle-* } }, flipy>(4, _))
		async(circle-red/w, <1;:>, gcrepeat {
			wait(60)
			times(_)
		} gsrepeat {
			times(60)
			circle
		} s(rvelocity(cx(4))))
		_(4)
			pool-control({ { circle-* } }, reset)
```

You can view all bullet controls here: <xref:Danmokou.Danmaku.BulletManager.SimpleBulletControls>, or in Danmaku/BulletManagement/BulletControls.cs:SimpleBulletControls.

The `bullet-control` command creates a bullet control. It takes three arguments:

- A predicate that runs every frame to check if the control should continue to exist. `Persist` is an alias for `True` and `Once` is an alias for `False`. If you set this to `Once`/`False`, the control will run exactly once.
- A style selector. We'll discuss this in a later part, but for now, just think of `{{ circle-* }}` as "select all circle bullet types".
- A control. We are using the FlipYGT (FlipY>) control, which takes two arguments:
  - A y-value "ceiling" against which to check flipping,
  - A predicate to filter which bullets get controlled. `_` is an alias for `True`, which means all bullets get controlled. 

Therefore, we have created a control that "flips all circle bullets when they go higher than y=4". 

There are several ways to destroy controls. If you want to clear all controls on a bullet style, you can use the reset pool control.

Before continuing, let's establish the difference between a bullet control and a pool control. 

- A bullet control is an object that persists over time and is applied to every bullet of a given style. 
- A pool control is a function that is run once on the **metadata** for a bullet style (ie. it does not affect individual bullets).

The `reset` pool control simply destroys all current bullet controls on a bullet style.

To use pool controls, we invoke the `pool-control` command, which takes a style selector and a pool control as arguments.

However, we don't want to destroy the control immediately. Instead, we use the delay command `_(delaySeconds)` to destroy it after four seconds.

Another way to destroy controls is to use timers.

```python
		timer(restart(pt))
		bullet-control(< timersec(pt) 4, { { circle-* } }, flipy>(4, _))
		async circle-red/w <1;:> gcrepeat {
			wait(60)
			times(_)
		} gsrepeat {
			times(60)
			circle
		} s(rvelocity(cx(4)))
```

`timer(restart(X))` sets the elapsed time of the timer X to zero and then starts recording. If the timer doesn't exist, it will be created. 

To delete the control after four seconds, we use a predicate that returns true if the timer's elapsed time, in seconds, is less than 4. (Note: BDSM does not have infix support for boolean operators such as `>`. These are treated as functions and must be used in prefix form; `timersec(pt) < 4` is written as `< timersec(pt) 4`.)

## Part 2: Style Selector 

The style selector is an array of arrays. If we take one element from each array and combine them according to wildcard rules, then we get a target style. The engine takes every possible combination and applies the control to all the applicable styles.

For example, let's say we have the selector `{ { circle-* ellipse-* } { red/w blue/w } }`. 

The engine generates:

```python
(circle-*, red/w)
(circle-*, blue/w)
(ellipse-*, red/w)
(ellipse-*, blue/w)
```

and then merges them using wildcard rules into

```python
circle-red/w
circle-blue/w
ellipse-red/w
ellipse-blue/w
```

To test your understanding, try this question:

1. Let's say we wanted to select circle and ellipses; in colors red, green, and blue; and in colorization methods /w and /b. What style selector should we use?

If you remember the previous example, we used a style selector `{{ circle-* }}`. There's one unresolved wildcard! If you try to summon a bullet with style `circle-*`, the engine will throw an error telling you that it can't find a simple bullet style with that name. However, in a style selector, you can leave one wildcard in the final style string, and it will match all existing styles that fit the format.  

Knowing this, answer this question:

2. Let's say we wanted to select circle and ellipses; in all colors; and in colorization methods /w and /b. What style selector should we use?

Now look at this code and try to figure out what it will do. Hint: The `cull` control deletes bullets if the condition is satisfied.

```python
		bullet-control(persist, { { circle-* ellipse-* } { red/w blue/w } }, cull(< y -2))
		async */w <1;:5> girepeat {
			wait(60)
			times(_)
			color({ *-red *-blue *-green })
		} gcrepeat {
			wait(10)
			times(3)
			color({ circle ellipse triangle })
			rv2incr(<2>)
		} gsrepeat {
			times(24)
			circle
		} s(rvelocity(cx(2)))
```

## Part 3: Fun With GTRepeat

Take a look at this code:

```python
	gtrepeat {
		wait(40)
		times(_)
		for(3s)
	} bullet-control(once, circle-red/w, batch(> t 1, {
		restyleeffect(arrow-red/b, cwheel-red/, _)
		sfx(x-transform-1, _)
	}))
	async circle-red/w <1;:2> gcrepeat {
		wait(10)
		times(_)
		for(4s)
		rv2incr(<6>)
	} gsrepeat {
		times(30)
		circle
	} s(rvelocity(cx(2)))
```
There are a few new features here, so let's go through them.

This time, we're actually using `once` (`False`) as the persistence predicate to bullet-control. This means that the control executes for exactly one frame.

We also introduce the `batch` command. This allows us to apply multiple controls with a single predicate, which is highly useful for complex effects. In this case, we want to change the style of any circle bullet older than 1 second, and also play a sound effect if this happens. 

The `restyleeffect` control takes three arguments: a target style, an effect style, and a predicate. In this case, we are transforming the bullets into arrows, so we provide the target style `arrow-red/b`. Effect styles are a special type of bullet style that you should not try to create directly. They are used for simple frame-animated particle effects, but because they are bullets and not actual particle effects, we can use tens of thousands of them simultaneously. For now, the only effect style is `cwheel`, so we'll use that. Finally, since the predicate `t is greater than 1` is already checked by the batch command, we don't need to put anything here, so we just use `_` (`True`).

The `sfx` control takes two arguments: a sound effect and a predicate. You can see the list of usable sound effects in GameManagement/SFXService. Click on any of the "SFX Config" objects and look at the "Default Name" property. Sound effects have a cooldown (see the "Timeout" property) which prevents them from getting invoked too many times at once. Even if this control is applied to a hundred bullets in one frame, it will only play the sound once. This is for ear safety. 

Now, we can describe the control as follows: "Every 40 frames for 3 seconds, for every circle-red/w bullet that is older than 1 second, transform it into an arrow-red/b while playing the sound effect x-transform-1."

## Part 4: The SM Control

All bullet controls are equal, but the SM control is the most equal of them all.

The SM control allows us to summon an invisible node (inode) at the location of a bullet and run an arbitrary state machine on it. This is generally how you "fire bullets from bullets", or perform single-to-many bullet transformations.

Let's start by summoning some spinning stars.

```python
		sync lstar-blue/w <> gsr {
			times(8)
			circle
			bindItr(nStar)
			expose({ f nStar })
		} simple(rvelocity(lerp(0.2, 1, t, cx 3, zero)), { 
			dir(200 * t)
		})
```

We've seen `bindItr` before, but what's this about `expose`? When we bind variables within repeater functions, by default they are **not included** in the bullet's private data when the bullet is fired. However, if you use the variable in the bullet firing function, then it will be automatically included. In this case, we are not using `nStar` in either the velocity equation or the direction equation below it, but we still want to send it to the bullet's private data for later usage. The `expose` modifier allows us to do this. It takes a list of `(type, variable)` pairs, and sends these to the bullet's private data. We'll discuss type abbreviations later, but in general you will either use `f` (float) or `v2` (vector2) or `rv2` (V2RV2). `nStar` is a float value, so we expose it as such.

We're also now using `simple` instead of `s`. This command allows us to provide an array of simple bullet options (see <xref:Danmokou.Danmaku.Options.SBOption> or Danmaku/BulletManagement/SimpleBulletOptions.cs), which are used for fancy effects. In our case, we want the stars to rotate independently of their movement, so we provide a custom direction function in degrees. The function `200 * t` makes the bullets rotate 200 degrees counterclockwise per second.

Also, our velocity function is now using some fancy math. The Lerp function allows us to linearly interpolate between two values. The signature of Lerp is `Lerp<T>(float zeroBound, float oneBound, float controller, T val1, T val2)`. When the controller is between `zeroBound` and `oneBound`, the return value is weighted between `val1` and `val2` depending on the proximity to each of the bounds.

 In other words, `lerp(0.2, 1, t, cx 3, zero)` means "go from speed (3, 0) to speed (0, 0) when time is between 0.2 and 1". 

Now, below the code you already wrote, let's add an SM control.

To make this fancier, let's make the stars burst one-by-one instead of all at the same time. We can use `nStar` in the control predicate to achieve this. Let's say the first star explodes when it is 1.2 seconds old, and the second at 1.5 seconds, and so on. We could write the condition as: `> t (1.2 + 0.3 * &nStar)`. 

...Well, not exactly. There's a minor issue with this, by design. In DMK, accessing bound variables like `&nStar` can occur in one of two contexts: *scoped* or *unscoped*. Scoped access occurs when a parent binds the variable and then a child tries to read it. For example, this example from the first tutorial is scoped, because gcrepeat binds the variable `&loop` and its child, s, tries to read it. 

```python
	async(arrow-red/w, <0;0:1;0:0>, gcrepeat({
			wait(10)
			times(10)
			rv2incr(<4>)
			bindItr(loop)
		}, s(rvelocity(pxy(1 + 0.4 * &loop, 0))))
	)
```

Unscoped access occurs when a third party tried to read the variable. In this case, the predicate in a bullet control function is unscoped. When accessing bound variables from an unscoped context, you must explicitly state your intent by writing it as `&.nStar` instead (prefix `&.` instead of `&`). This is done for scripting safety, because accessing nonexistent variables from an unscoped context is very hard to debug and will raise a lot of strange errors. 

If you didn't understand that, it's fine. Don't worry about it. Drop this control function under the star code:

```python
		bullet-control(persist, lstar-blue/w, batch( > t (1.2 + 0.3 * &.nStar), {
			sm(_, sync star-*/w <> gsrepeat {
					circle
					times(22)
					sfx(x-fire-burst-1)
					colorf({ red pink purple blue teal green yellow orange }, &nStar)
				} simple(rvelocity(lerp(0.2, 1, t, cx 2, cx 4)), {
					dir(starrotb3)
				}))
			softcull(cwheel-blue/w, _)
		}))
```

The SM control takes two arguments: a predicate, and a StateMachine to execute. The predicate comes first due to my formatting preferences, but feel free to change it. It's as easy as changing the argument order of the SM function in the file Danmaku/BulletManagement/BulletControls.cs (though you'll break many of the example scripts if you do that).

The StateMachine here is nothing too new. We use the &nStar variable, which is passed from the bullet data to the inode, to determine the color of the small stars. We also use the `starrot3` rotation function. The `starrotX` and `starrotbX` functions in <xref:Danmokou.DMath.Functions.BPYRepo> give slightly randomized rotation functions in one or two directions respectively. 

We also use the `softcull` control. This is almost the same as the `cull` control, but in addition to deleting the bullet, it also spawns an effect bullet at the position. 

There's only one control that's as interesting as SM, which is Update. However, Update is too complex for the second tutorial, so we'll come back to it another time. That's all for this tutorial!

## Problem Solutions

1. `{ circle-** ellipse-** } { red blue green } { /w /b }`
2. `{ circle-* ellipse-* } { */w */b }`